MODULE OfrontOPM;	(* RC 6.3.89 / 28.6.89, J.Templ 10.7.89 / 8.5.95*)
(* constants needed for C code generation *)
(*
2002-04-11  jt: Number changed for BlackBox 1.4: SHORT(-e)
2002-08-15  jt: Scan: path separator changed from blank to semicolon
2002-08-21  jt: CloseFiles: inC tested for NIL
*)

	IMPORT SYSTEM, Strings, Files, Models, Out, Stores, TextControllers, TextModels, TextMappers,
		TextViews, Views, Dialog, Fonts, Ports, Controllers, Properties, DevMarkers, Windows, MultiCardinals;

	CONST
		OptionChar* = "-";

		(* compiler options; don't change the encoding *)
		inxchk* = 0;	(* index check on *)
		vcpp* = 1;	(* VC++ support on; former ovflchk; neither used nor documented *)
		ranchk* = 2;	(* range check on *)
		typchk* = 3;	(* type check on *)
		newsf* = 4;	(* generation of new symbol file allowed *)
		ptrinit* = 5;	(* pointer initialization *)
		ansi* = 6;	(* ANSI or K&R style prototypes *)
		assert* = 7;	(* assert evaluation *)
		include0* = 8;	(* include M.h0 in header file and M.c0 in body file if such files exist *)
		extsf* = 9;	(* extension of old symbol file allowed *)
		mainprog* = 10;	(* translate module body into C main function *)
		dynlib* = 11;	(* translate module to be a dynamic library *)
		defopt* = {inxchk, typchk, ptrinit, assert, ansi};	(* default options *)

		nilval* = 0;

		MaxRExp* = 38;	MaxLExp* = 308;

		MinHaltNr* = 0;
		MaxHaltNr* = 255;
		MaxSysFlag* = 1;

		MaxCC* = -1;	(* SYSTEM.CC, GETREG, PUTREG; not implementable in Ofront *)
		MinRegNr* = 0;
		MaxRegNr* = -1;

		LANotAlloc* = -1;	(* XProc link adr initialization *)
		ConstNotAlloc* = -1;	(* for allocation of string and real constants *)
		TDAdrUndef* = -1;	(* no type desc allocated *)

		MaxCases* = 256;
		MaxCaseRange* = 512;

		MaxStruct* = 1282;

		(* maximal number of pointer fields in a record: *)
		MaxPtr* = MAX(INTEGER);

		(* maximal number of global pointers per module: *)
		MaxGPtr* = MAX(INTEGER);

		(* maximal number of hidden fields in an exported record: *)
		MaxHdFld* = 512;

		HdPtrName* = "@ptr";
		HdProcName* = "@proc";
		HdTProcName* = "@tproc";

		ExpHdPtrFld* = TRUE;
		ExpHdProcFld* = FALSE;
		ExpHdTProc* = FALSE;

		NEWusingAdr* = FALSE;

		Eot* = 0X;

		SFext = ".sym";	(* symbol file extension *)
		BFext = ".c";	(* body file extension *)
		HFext = ".h";	(* header file extension *)
		SFtag = 0F7X;	(* symbol file tag *)

		HeaderFile* = 0;
		BodyFile* = 1;
		HeaderInclude* = 2;

	VAR
		SetSize-, IndexSize-, MaxSet-: SHORTINT; MaxIndex-: LONGINT;

		noerr-, warning156-: BOOLEAN;
		curpos-, errpos*: INTEGER;	(* character and error position in source file *)
		breakpc-: INTEGER;	(* set by OPV.Init *)
		currFile*: INTEGER;	(* current output file *)
		level*: INTEGER;	(* procedure nesting level *)
		pc-, entno-: INTEGER;  (* entry number *)
		modName-: ARRAY 32 OF SHORTCHAR;
		objname*: ARRAY 64 OF SHORTCHAR;

		opt-, glbopt: SET;
		GlobalLanguage, Language-: SHORTCHAR; (* "1", "2": S8/I16/L32 | "C", "3": S16/I32/L64 *)
		GlobalAddressSize, AddressSize-, GlobalAlignment, Alignment-: SHORTINT;

		lasterrpos: INTEGER;
		inR: TextModels.Reader;
		inC: TextControllers.Controller;
		script: Stores.Operation; (* for undoing insert error marks *)
		inS: TextMappers.Scanner;
		inV: Views.View;
		inLoc: Files.Locator;
		inName: Files.Name;
		end: INTEGER;
		translateList: BOOLEAN;
		oldSF: Stores.Reader; newSF: Stores.Writer;
		R: ARRAY 3 OF Files.Writer;
		workDir, subsys: Files.Name;
		SFsubdir, CFsubdir: ARRAY 5 + 2 OF CHAR;
		oldSFile, newSFile, HFile, BFile, HIFile: Files.File;
		stop: BOOLEAN;

		options*: RECORD
			dirs*: ARRAY 256 OF CHAR;
			mainprog*: BOOLEAN;
			par: POINTER TO ARRAY OF CHAR
		END;

		locate*: RECORD
			num*: INTEGER
		END;

	(* ------------------------- System runtime ------------------------- *)

	TYPE
		LongCard  =  LONGINT;

	PROCEDURE LongCardDiv*(c, m: LongCard): LongCard;
	BEGIN
		RETURN MultiCardinals.Div(c, m)
	END LongCardDiv;

	PROCEDURE Div0*(x, y: LONGINT): LONGINT;
	BEGIN
		ASSERT(y # 0);
		IF (x < 0) = (y < 0) THEN RETURN x DIV y
		ELSIF x < 0 THEN RETURN -x DIV (-y)
		ELSE RETURN -(-x) DIV y
		END
	END Div0;

	PROCEDURE Rem0*(x, y: LONGINT): LONGINT;
	BEGIN
		ASSERT(y # 0);
		IF (x < 0) = (y < 0) THEN RETURN x MOD y
		ELSIF x < 0 THEN RETURN x MOD (-y)
		ELSE RETURN -(-x) MOD y
		END
	END Rem0;

	(* -------------------------  lookup with search path ------------------------- *)

	PROCEDURE Scan(VAR dirs, dir: ARRAY OF CHAR; VAR pos: INTEGER);
		VAR i: INTEGER; ch: CHAR;
	BEGIN ch := dirs[pos]; i := 0;
		WHILE ch = ';' DO INC(pos); ch := dirs[pos] END ;
		WHILE (ch # 0X) & (ch # ';') DO dir[i] := ch; INC(i); INC(pos); ch := dirs[pos] END ;
		dir[i] := 0X;
		Strings.Find(dir, "%WorkDir%", 0, i); IF i # -1 THEN Strings.Replace (dir, i, 9, workDir) END;
		Strings.Find(dir, "%Subsys%", 0, i); IF i # -1 THEN Strings.Replace (dir, i, 8, subsys) END;
	END Scan;

	PROCEDURE FilesNew(IN subdir: ARRAY OF CHAR): Files.File;
		VAR dirs, dir: ARRAY 256 OF CHAR; pos: INTEGER;
	BEGIN
		Dialog.MapString(options.dirs, dirs);
		pos := 0; Scan(dirs, dir, pos);
		RETURN Files.dir.New(Files.dir.This(dir + subdir), Files.ask)
	END FilesNew;

	PROCEDURE FilesOld(IN name: Files.Name; IN subdir: ARRAY OF CHAR): Files.File;
		VAR dirs, dir: ARRAY 256 OF CHAR; pos: INTEGER; F: Files.File;
	BEGIN
		Dialog.MapString(options.dirs, dirs);
		pos := 0; Scan(dirs, dir, pos);
		REPEAT
			F := Files.dir.Old(Files.dir.This(dir + subdir), name, Files.shared); Scan(dirs, dir, pos)
		UNTIL (F # NIL) OR (dir = "");
		RETURN F
	END FilesOld;

	PROCEDURE FilesDelete(IN name: Files.Name; IN subdir: ARRAY OF CHAR);
		VAR dirs, dir: ARRAY 256 OF CHAR; pos: INTEGER;
	BEGIN
		Dialog.MapString(options.dirs, dirs);
		pos := 0; Scan(dirs, dir, pos);
		Files.dir.Delete(Files.dir.This(dir + subdir), name)
	END FilesDelete;

	PROCEDURE ViewsOld(IN name: ARRAY OF CHAR): Views.View;
		VAR dirs, dir: ARRAY 256 OF CHAR; pos: INTEGER;
	BEGIN
		Dialog.MapString(options.dirs, dirs);
		pos := 0; Scan(dirs, dir, pos);
		inName := name$;
		REPEAT
			inLoc := Files.dir.This(dir);
			inV := Views.OldView(inLoc, inName);
			Scan(dirs, dir, pos)
		UNTIL (inV # NIL) OR (dir = "");
		RETURN inV
	END ViewsOld;

	(* ------------------------- Log Output ------------------------- *)

	PROCEDURE LogW*(ch: CHAR);
	BEGIN
		Out.Char(ch)
	END LogW;

	PROCEDURE LogWStr*(IN s: ARRAY OF SHORTCHAR);
	BEGIN
		Out.String(LONG(s));
	END LogWStr;

	PROCEDURE LogWNum*(i, len: INTEGER);
	BEGIN
		Out.Int(i, len)
	END LogWNum;

	PROCEDURE LogWLn*;
	BEGIN
		Out.Ln
	END LogWLn;


	(* ------------------------- initialization ------------------------- *)

	PROCEDURE GetAlignment(VAR a: SHORTINT);
	BEGIN a := 8	(* Windows x86/Linux ARM = 48, Linux x86 = 44, x64 = 88 *)
	END GetAlignment;

	PROCEDURE GetProperties;
	BEGIN
		warning156 := AddressSize = -1;
		IF warning156 THEN	(* this compiler's address size and alignment *)
			AddressSize := SIZE(SYSTEM.PTR); GetAlignment(Alignment)
		END;
		(* default characteristics *)
		IF AddressSize = 2 THEN	(* Z80 and other 8 bit CPUs *)
			SetSize := 1; MaxSet := 7; IndexSize := 2; MaxIndex := MAX(SHORTINT)
		ELSE
			SetSize := 4; MaxSet := 31;
			IndexSize := 4;	(* size of the type of array index, see also SYSTEM_ARRLEN *)
			MaxIndex := MAX(INTEGER)
		END
	END GetProperties;

	(* ------------------------- parameter handling -------------------------*)

	PROCEDURE ScanOptions(VAR s: ARRAY OF CHAR; VAR opt: SET);
		VAR i: INTEGER;
	BEGIN i := 0;
		WHILE s[i] # 0X DO
			CASE s[i] OF
			| "e": opt := opt / {extsf}
			| "s": opt := opt / {newsf}
			| "m": opt := opt / {mainprog}
			| "x": opt := opt / {inxchk}
			| "v": opt := opt / {vcpp, ansi};
			| "r": opt := opt / {ranchk}
			| "t": opt := opt / {typchk}
			| "a": opt := opt / {assert}
			| "k": opt := opt / {ansi}
			| "p": opt := opt / {ptrinit}
			| "i": opt := opt / {include0}
			| "d": opt := opt / {dynlib}
			| "1", "3", "C", "7"(*undocumented*): Language := SHORT(s[i])

			(* target machine address size and alignment
					"21": 16 bit addresses, SIZE(SET) = 1 byte (e.g. Zilog Z80 CPU).
					"44": 32 bit addresses, 32 bit alignment (e.g. Unix/Linux 32 bit on x86).
					"48": 32 bit addresses, 64 bit alignment (e.g. Windows 32 bit on x86, Linux 32 bit on ARM).
					"88": 64 bit addresses, 64 bit alignment (e.g. 64 bit platforms).
			*)
			| "2":
				IF s[i+1] = "1" THEN AddressSize := 2; Alignment := 1; INC(i)
				ELSE Language := "2"
				END
			| "4":
				IF s[i+1] = "4" THEN AddressSize := 4; Alignment := 4; INC(i)
				ELSIF s[i+1] = "8" THEN AddressSize := 4; Alignment := 8; INC(i)
				ELSE
					LogWStr("  warning: option "); LogW(OptionChar); LogW("4");
					IF s[i+1] # 0X THEN LogW(s[i+1]); INC(i) END;
					LogWStr(" ignored"); LogWLn
				END
			| "8":
				IF s[i+1] = "8" THEN AddressSize := 8; Alignment := 8; INC(i)
				ELSE
					LogWStr("  warning: option "); LogW(OptionChar); LogW("8");
					IF s[i+1] # 0X THEN LogW(s[i+1]); INC(i) END;
					LogWStr(" ignored"); LogWLn
				END

			ELSE LogWStr("  warning: option "); LogW(OptionChar); LogW(s[i]); LogWStr(" ignored"); LogWLn
			END ;
			INC(i)
		END
	END ScanOptions;

	PROCEDURE GetOptionsAt(pos: INTEGER; VAR opt: SET);
		VAR respos: INTEGER; ch: CHAR; module, done: BOOLEAN; optstr: ARRAY 256 OF CHAR;
	BEGIN
		respos := 0; module := TRUE; done := FALSE;
		WHILE ~done & (pos < LEN(options.par^)) DO
			ch := options.par^[pos];
			IF ch = 0X THEN done := TRUE
			ELSIF ch <= " " THEN module := TRUE
			ELSIF module & (ch = OptionChar) THEN module := FALSE
			ELSIF ~module THEN optstr[respos] := ch; INC(respos)
			ELSE done := TRUE
			END;
			INC(pos)
		END;
		optstr[respos] := 0X; ScanOptions(optstr, opt)
	END GetOptionsAt;

	PROCEDURE GetModuleOptions(VAR opt: SET);
		VAR pos, endpos: INTEGER; source: TextModels.Model; w: Windows.Window;
	BEGIN
		IF options.par # NIL THEN
			source := TextViews.FocusText();
			IF source # NIL THEN
				w := Windows.dir.Focus(Controllers.targetPath);
				IF (w # NIL) & (w.loc # NIL) & (w.name # "") THEN
					pos := 0;
					REPEAT
						Strings.Find(options.par^, w.name, pos, pos);
						IF pos # -1 THEN
							endpos := pos + LEN(w.name$);
							IF (pos > 0) & (options.par^[pos - 1] > " ") THEN INC(pos)
							ELSIF (endpos < LEN(options.par^$)) & (options.par^[endpos] > " ") THEN INC(pos)
							ELSE (* found *)
								GetOptionsAt(endpos, opt); pos := -1
							END
						END
					UNTIL pos = -1
				END
			END
		END
	END GetModuleOptions;

	PROCEDURE OpenPar*(list: BOOLEAN);	(* prepare for a sequence of translations *)
		VAR ch: SHORTCHAR; pos, beg: INTEGER; c: TextControllers.Controller;
			F: Files.File; R: Files.Reader; optstr: ARRAY 256 OF CHAR;
	BEGIN
		(* set options to initial defaults *)
		Language := "C";	(* default input language: Component Pascal *)
		AddressSize := -1;	(* undefined *)
		glbopt := defopt;
		translateList := list; stop := FALSE;
		c := TextControllers.Focus();  (* get the focus controller, if any *)
		IF c # NIL THEN
			IF translateList THEN
				IF c.HasSelection() THEN c.GetSelection(beg, end);
					inS.ConnectTo(c.text); inS.SetPos(beg); inS.Scan;
					IF (inS.type = TextMappers.char) & (inS.char = "/") THEN
						pos := inS.start; inS.Scan;
						IF (inS.start = pos + 1) & (inS.type = TextMappers.string) THEN
							ScanOptions(inS.string, glbopt); inS.Scan
						END
					END
				ELSE stop := TRUE
				END
			ELSE
				F := FilesOld("Ofront+.par", CFsubdir);
				IF F # NIL THEN
					R := F.NewReader(NIL); R.SetPos(0); NEW(options.par, F.Length() + 1);
					pos := 0;
					WHILE (~R.eof) & (R.Pos() < F.Length()) DO
						R.ReadByte(SYSTEM.VAL(BYTE, ch)); options.par^[pos] := ch; INC(pos)
					END;
					options.par^[pos] := 0X; GetOptionsAt(0, glbopt)
				ELSE
					options.par := NIL
				END
			END
		ELSE stop := TRUE
		END;
		(* Record global option settings for this command line *)
		GlobalAddressSize := AddressSize; GlobalAlignment := Alignment; GlobalLanguage := Language
	END OpenPar;

	PROCEDURE InitOptions*;	(* get the options for one translation *)
		VAR pos: INTEGER;
	BEGIN
		Alignment := GlobalAlignment; AddressSize := GlobalAddressSize; Language := GlobalLanguage;
		opt := glbopt;
		IF translateList & (inS.start < end) & (inS.type = TextMappers.char) & (inS.char = "/") THEN
			pos := inS.start; glbopt := defopt; inS.Scan;
			IF (inS.start = pos + 1) & (inS.type = TextMappers.string) THEN
				ScanOptions(inS.string, opt); inS.Scan
			END
		ELSIF ~translateList THEN GetModuleOptions(opt)
		END;
		GetProperties; IF options.mainprog THEN INCL(opt, mainprog) END
	END InitOptions;

	PROCEDURE Init*(VAR done: BOOLEAN);	(* get the source for one translation *)
		VAR v: Views.View; m: Models.Model; source: TextModels.Model; E: Views.View; pos: INTEGER;
	BEGIN
		done := FALSE; curpos := 0;
		IF stop THEN RETURN END;
		IF translateList THEN
			IF (inS.start < end) & (inS.type = TextMappers.string) THEN
				LogWStr(SHORT(inS.string));
				v := ViewsOld(inS.string);
				IF v # NIL THEN
					m := v.ThisModel();
					IF (m # NIL) & (m IS TextModels.Model) THEN
						LogWStr("  translating");
						source := m(TextModels.Model);
						inR := source.NewReader(inR); inR.SetPos(0);
						done := TRUE; inS.Scan
					ELSE LogWStr(" not a text document"); LogWLn
					END
				ELSE LogWStr(" not found"); LogWLn
				END
			END
		ELSE
			inC := TextControllers.Focus();  (* get the focus controller, if any *)
			IF inC # NIL THEN  (* a text view must be focus *)
				LogWStr("translating");
				inR := inC.text.NewReader(inR); inR.SetPos(0); inR.ReadView(E);
				WHILE E # NIL DO pos := inR.Pos();
					IF E IS DevMarkers.View THEN
						inC.text.Delete(pos - 1, pos);
						inR.SetPos(pos-1)
					END;
					inR.ReadView(E)
				END ;
				inR.SetPos(0);
				Models.BeginScript(inC.text, "#ofront:insertMarkers", script);
				done := TRUE; stop := TRUE
			END
		END ;
		level := 0; noerr := TRUE; errpos := curpos; lasterrpos := curpos - 10
	END Init;

	PROCEDURE AppInt(x, pos: INTEGER; VAR s: ARRAY OF CHAR);
	BEGIN
		IF x > 100 THEN s[pos] := CHR(ORD("0") + x DIV 100); INC(pos) END ;
		IF x > 10 THEN s[pos] := CHR(ORD("0") + x MOD 100 DIV 10); INC(pos) END ;
		s[pos] := CHR(ORD("0") + x MOD 10); INC(pos); s[pos] := 0X
	END AppInt;


	PROCEDURE LocatePos*;
		VAR C: TextControllers.Controller; pos: INTEGER;
	BEGIN
		C := TextControllers.Focus();
		IF C = NIL THEN RETURN END ;
		IF locate.num < 0 THEN pos := 0; Dialog.Beep
		ELSIF locate.num > C.text.Length() THEN pos := C.text.Length(); Dialog.Beep
		ELSE pos := locate.num
		END ;
		C.view.ShowRange(pos, pos, FALSE);
		C.SetCaret(pos)
	END LocatePos;

	PROCEDURE LocateLine*;
		VAR C: TextControllers.Controller; R: TextModels.Reader; ch: CHAR; line, pos: INTEGER;
	BEGIN
		C := TextControllers.Focus();
		IF C = NIL THEN RETURN END ;
		R := C.text.NewReader(NIL);
		R.SetPos(0); line := 0;
		REPEAT
			R.ReadChar(ch);
			IF ch = TextModels.line THEN INC(line) END
		UNTIL R.eot OR (line = locate.num);
		IF R.eot THEN pos := C.text.Length(); Dialog.Beep
		ELSIF locate.num < 0 THEN pos := 0; Dialog.Beep
		ELSE pos := R.Pos() - 1
		END ;
		C.view.ShowRange(pos, pos, FALSE);
		C.SetCaret(pos)
	END LocateLine;


	(* ------------------------- read source text -------------------------*)

	PROCEDURE Get*(VAR ch: SHORTCHAR);	(* read next character from source text, 0X if eof *)
	BEGIN
		inR.Read();
		IF inR.eot THEN ch := 0X
		ELSE ch := SHORT(inR.char); INC(curpos)
		END
	END Get;

	PROCEDURE MakeFileName(IN name: ARRAY OF SHORTCHAR; VAR FName: Files.Name; IN ext: ARRAY OF CHAR);
		VAR i, j: INTEGER; ch: CHAR;
	BEGIN i := 0;
		LOOP ch := name[i];
			IF ch = 0X THEN EXIT END ;
			FName[i] := ch; INC(i)
		END ;
		j := 0;
		REPEAT ch := ext[j]; FName[i] := ch; INC(i); INC(j)
		UNTIL ch = 0X
	END MakeFileName;

	PROCEDURE Mark*(n: SHORTINT; pos: INTEGER);
		VAR E: DevMarkers.View; T: TextModels.Model; W: TextModels.Writer; s: ARRAY 256 OF CHAR;
	BEGIN
		IF n >= 0 THEN
			IF pos < 0 THEN pos := 0 END ;
			IF (pos < lasterrpos) OR (lasterrpos + 9 < pos) THEN lasterrpos := pos;
				IF n < 249 THEN
					IF inC = NIL THEN RETURN END;
					E := DevMarkers.dir.New(DevMarkers.message);
					Strings.IntToString(n, s); Dialog.MapString("#ofront:"+s, s);
					E.InitErr(n); E.InitMsg(s);
					T := inR.Base(); W := T.NewWriter(NIL); W.SetPos(pos);
					W.WriteView(E, 10*Ports.point, 10 * Ports.point);
					INC(curpos); inR.SetPos(curpos);
					IF noerr THEN
						IF translateList THEN Views.Open(inV, inLoc, inName, NIL);
							inV(TextViews.View).ShowRange(pos, pos, FALSE)
						ELSE inC.view.ShowRange(pos, pos, FALSE)
						END
					END
				ELSE
					s := "#ofront:"; AppInt(n, 8, s);
					Dialog.MapParamString(s, LONG(objname), "", "", s);
					LogWLn; LogWStr("  "); LogWStr(SHORT(s))
				END
			END ;
			noerr := FALSE
		ELSE
			IF pos >= 0 THEN LogWLn; LogWStr("  pos"); LogWNum(pos, 6) END ;
			LogWStr("  warning  "); s := "#ofront:"; AppInt(-n, 8, s);
			Dialog.MapString(s, s); LogWStr('"'); LogWStr(SHORT(s)); LogWStr('"');
			IF pos < 0 THEN LogWLn END
		END
	END Mark;


	PROCEDURE err*(n: SHORTINT);
	BEGIN Mark(n, errpos)
	END err;

	PROCEDURE FPrint*(VAR fp: INTEGER; val: INTEGER);
	BEGIN
		fp := SYSTEM.ROT(SYSTEM.VAL(INTEGER, SYSTEM.VAL(SET, fp) / SYSTEM.VAL(SET, val)), 1)
	END FPrint;

	PROCEDURE FPrintSet*(VAR fp: INTEGER; set: SET);
	BEGIN FPrint(fp, SYSTEM.VAL(INTEGER, set))
	END FPrintSet;

	PROCEDURE FPrintReal*(VAR fp: INTEGER; real: SHORTREAL);
	BEGIN FPrint(fp, SYSTEM.VAL(INTEGER, real))
	END FPrintReal;

	PROCEDURE FPrintLReal*(VAR fp: INTEGER; lr: REAL);
		VAR li: LONGINT;
	BEGIN li := SYSTEM.VAL(LONGINT, lr);
		FPrint(fp, SHORT(li)); FPrint(fp, SHORT(ASH(li, -32)))
	END FPrintLReal;

	(* ------------------------- Read Symbol File ------------------------- *)

	PROCEDURE SymRCh*(VAR ch: SHORTCHAR);
	BEGIN
		oldSF.ReadSChar(ch)
	END SymRCh;

	PROCEDURE SymRInt* (): LONGINT;
		VAR s, byte: BYTE; ch: SHORTCHAR; n: LONGINT;
	BEGIN s := 0; n := 0; oldSF.ReadByte(byte); ch := SHORT(CHR(byte));
		WHILE ORD(ch) >= 128 DO INC(n, ASH(LONG(ORD(ch) - 128), s) ); INC(s, 7);
			oldSF.ReadByte(byte); ch := SHORT(CHR(byte))
		END;
		INC(n, ASH(LONG(ORD(ch) MOD 64 - ORD(ch) DIV 64 * 64), s) );
		RETURN n
	END SymRInt;

	PROCEDURE SymRSet*(VAR s: SET);
	BEGIN
		s := SYSTEM.VAL(SET, SHORT(SymRInt()))
	END SymRSet;

	PROCEDURE SymRReal*(VAR r: SHORTREAL);
	BEGIN
		oldSF.ReadSReal(r)
	END SymRReal;

	PROCEDURE SymRLReal*(VAR lr: REAL);
	BEGIN
		oldSF.ReadReal(lr)
	END SymRLReal;

	PROCEDURE CloseOldSym*;
	BEGIN oldSFile.Close; oldSFile := NIL; oldSF.ConnectTo(NIL)
	END CloseOldSym;

	PROCEDURE OldSym*(IN modName: ARRAY OF SHORTCHAR; VAR done: BOOLEAN);
		VAR ch: SHORTCHAR; fileName: Files.Name;
	BEGIN MakeFileName(modName, fileName, SFext);
		oldSFile := FilesOld(fileName, SFsubdir); done := oldSFile # NIL;
		IF done THEN
			oldSF.ConnectTo(oldSFile); oldSF.ReadSChar(ch);
			IF ch # SFtag THEN err(-306);  (*possibly a symbol file from another Oberon implementation, e.g. HP-Oberon*)
				CloseOldSym; done := FALSE
			END
		END
	END OldSym;

	PROCEDURE eofSF*(): BOOLEAN;
	BEGIN
		RETURN oldSF.rider.eof
	END eofSF;

	(* ------------------------- Write Symbol File ------------------------- *)

	PROCEDURE SymWCh*(ch: SHORTCHAR);
	BEGIN
		newSF.WriteSChar(ch)
	END SymWCh;

	PROCEDURE SymWInt* (x: LONGINT);
	BEGIN
		WHILE (x < - 64) OR (x > 63) DO newSF.WriteByte(SHORT(SHORT(SHORT(x) MOD 128 + 128))); x := x DIV 128 END;
		newSF.WriteByte(SHORT(SHORT(SHORT(x) MOD 128)))
	END SymWInt;

	PROCEDURE SymWSet*(s: SET);
	BEGIN
		SymWInt(SYSTEM.VAL(INTEGER, s))
	END SymWSet;

	PROCEDURE SymWReal*(r: SHORTREAL);
	BEGIN
		newSF.WriteSReal(r)
	END SymWReal;

	PROCEDURE SymWLReal*(lr: REAL);
	BEGIN
		newSF.WriteReal(lr)
	END SymWLReal;

	PROCEDURE RegisterNewSym*;
		VAR fileName: Files.Name; res: INTEGER;
	BEGIN
		IF (modName # "SYSTEM") OR (mainprog IN opt) THEN
			MakeFileName(modName, fileName, SFext);
			newSFile.Register(fileName, "*", Files.dontAsk, res)
		END
	END RegisterNewSym;

	PROCEDURE DeleteNewSym*;
	END DeleteNewSym;

	PROCEDURE NewSym*(VAR modName: ARRAY OF SHORTCHAR);
		VAR fileName: Files.Name;
	BEGIN MakeFileName(modName, fileName, SFext);
		newSFile := FilesNew(SFsubdir);
		IF newSFile # NIL THEN
			newSF.ConnectTo(newSFile); newSF.WriteSChar(SFtag)
		ELSE err(153)
		END
	END NewSym;

	(* ------------------------- Write Header & Body Files ------------------------- *)

	PROCEDURE Write*(ch: SHORTCHAR);
	BEGIN
		R[currFile].WriteByte(SYSTEM.VAL(BYTE, ch))
	END Write;

	PROCEDURE WriteString*(IN s: ARRAY OF SHORTCHAR);
		VAR i: INTEGER; ch: SHORTCHAR;
	BEGIN i := 0; ch := s[0];
	WHILE ch # 0X DO Write(ch); INC(i); ch := s[i] END
	END WriteString;

	PROCEDURE WriteHex* (i: INTEGER);
		VAR s: ARRAY 3 OF SHORTCHAR;
			digit : INTEGER;
	BEGIN
		digit := SHORT(i) DIV 16;
		IF digit < 10 THEN s[0] := SHORT(CHR (ORD ("0") + digit)); ELSE s[0] := SHORT(CHR (ORD ("a") - 10 + digit)); END;
		digit := SHORT(i) MOD 16;
		IF digit < 10 THEN s[1] := SHORT(CHR (ORD ("0") + digit)); ELSE s[1] := SHORT(CHR (ORD ("a") - 10 + digit)); END;
		s[2] := 0X;
		WriteString(s)
	END WriteHex;

	PROCEDURE WriteInt* (i: LONGINT);
		VAR s: ARRAY 20 OF CHAR; i1: LONGINT; k: INTEGER;
	BEGIN
		IF (i = MIN(INTEGER)) OR (i = MIN(LONGINT)) THEN	(* requires special bootstrap for 64 bit *)
			(* ABS(MIN(INTEGER)) is one more than MAX(INTEGER), causing problems representing the value
				as a minus sign followed by absoute value. Therefore represent as -MaxInt - 1.
				For INTEGER this avoids a compiler warning 'this decimal constant is unsigned only in ISO C90'.
				For LONGINT it is the only way to represent MinLInt. *)
			Write("("); WriteInt(i+1); WriteString("-1)")
		ELSE i1 := ABS(i);
			s[0] := CHR(i1 MOD 10 + ORD("0")); i1 := i1 DIV 10; k := 1;
			WHILE i1 > 0 DO s[k] := CHR(i1 MOD 10 + ORD("0")); i1 := i1 DIV 10; INC(k) END ;
			IF i < 0 THEN s[k] := "-"; INC(k) END ;
			WHILE k > 0 DO DEC(k); Write(SHORT(s[k])) END;
			IF (ansi IN opt) & ((i < MIN(INTEGER)) OR (i > MAX(INTEGER))) THEN WriteString("LL") END
		END ;
	END WriteInt;

	PROCEDURE WriteReal* (r: REAL; suffx: CHAR);
		VAR s: ARRAY 32 OF CHAR; ch: CHAR; i: INTEGER;
	BEGIN
		IF ABS(r) > 1.0E+308 THEN Write("("); WriteReal(r / 2, suffx); WriteString(" * 2)"); RETURN END;
(*should be improved *)
		IF (r < MAX(LONGINT)) & (r > MIN(LONGINT)) & (r = ENTIER(r)) THEN
			IF suffx = "f" THEN WriteString("(SHORTREAL)") ELSE WriteString("(REAL)") END;
			WriteInt(ENTIER(r))
		ELSE
			IF suffx = "f" THEN Strings.RealToString(SHORT(r), s) ELSE Strings.RealToString(r, s) END;
(* s[i] := suffx; s[i+1] := 0X;
suffix does not work in K&R *)
			i := 0; ch := s[0];
			WHILE (ch # "D") & (ch # 0X) DO INC(i); ch := s[i] END ;
			IF ch = "D" THEN s[i] := "e" END ;
			WriteString(SHORT(s))
		END
	END WriteReal;

	PROCEDURE WriteLn* ();
	BEGIN
		R[currFile].WriteByte(0DH); R[currFile].WriteByte(0AH);
	END WriteLn;

	PROCEDURE WriteModPos*;
	BEGIN
		Write('"'); WriteString(modName); WriteString('", '); WriteInt(-errpos)
	END WriteModPos;

	PROCEDURE Append(VAR R: Files.Writer; F: Files.File);
		VAR R1: Files.Reader; buffer: ARRAY 4096 OF BYTE; oldPos: INTEGER;
	BEGIN
		IF F # NIL THEN
			R1 := F.NewReader(NIL); oldPos := R1.Pos(); R1.ReadBytes(buffer, 0, LEN(buffer));
			WHILE R1.Pos() # oldPos DO
				R.WriteBytes(buffer, 0, R1.Pos() - oldPos); oldPos := R1.Pos();
				R1.ReadBytes(buffer, 0, LEN(buffer))
			END
		END
	END Append;

	PROCEDURE OpenFiles*(VAR moduleName: ARRAY OF SHORTCHAR);
		VAR FName: Files.Name;
	BEGIN
		modName := moduleName$;
		HFile := FilesNew(CFsubdir);
		IF HFile # NIL THEN R[HeaderFile] := HFile.NewWriter(NIL) ELSE err(153) END;
		MakeFileName(moduleName, FName, BFext);
		BFile := FilesNew(CFsubdir);
		IF BFile # NIL THEN R[BodyFile] := BFile.NewWriter(NIL) ELSE err(153) END ;
		MakeFileName(moduleName, FName, HFext);
		HIFile := FilesNew(CFsubdir);
		IF HIFile # NIL THEN R[HeaderInclude] := HIFile.NewWriter(NIL) ELSE err(153) END ;
		IF include0 IN opt THEN
			MakeFileName(moduleName, FName, ".h0"); Append(R[HeaderInclude], FilesOld(FName, CFsubdir));
			MakeFileName(moduleName, FName, ".c0"); Append(R[BodyFile], FilesOld(FName, CFsubdir))
		END
	END OpenFiles;

	PROCEDURE FileDiff(name: Files.Name; new: Files.File): BOOLEAN;
		VAR old: Files.File; RO, RN: Files.Reader;
			bo, bn: BYTE; i, pos: INTEGER;
			dirs, dir: ARRAY 256 OF CHAR;
	BEGIN
		Dialog.MapString(options.dirs, dirs); pos := 0; Scan(dirs, dir, pos);
		old := Files.dir.Old(Files.dir.This(dir), name, Files.shared);
		IF (old = NIL) OR (old.Length() # new.Length()) THEN RETURN TRUE END ;
		RO := old.NewReader(NIL); RN := new.NewReader(NIL);
		FOR i := 0 TO old.Length() - 1 DO
			RO.ReadByte(bo); RN.ReadByte(bn);
			IF bo # bn THEN RETURN TRUE END
		END ;
		RETURN FALSE
	END FileDiff;

	PROCEDURE CloseFiles*;
		VAR FName: Files.Name; res: INTEGER;
	BEGIN
		IF noerr THEN LogWStr("    "); LogWNum(R[BodyFile].Pos(), 0); Dialog.ShowStatus("#ofront:ok")
		ELSE Dialog.ShowStatus("#ofront:error")
		END;
		IF noerr THEN
			IF modName = "SYSTEM" THEN
				IF ~(mainprog IN opt) THEN MakeFileName(modName, FName, BFext); BFile.Register(FName, "*", Files.dontAsk, res) END
			ELSIF ~(mainprog IN opt) THEN
				Append(R[HeaderInclude], HFile);
				MakeFileName(modName, FName, HFext);
				IF FileDiff(FName, HIFile) THEN HIFile.Register(FName, "*", Files.dontAsk, res) END ;
				MakeFileName(modName, FName, BFext); BFile.Register(FName, "*", Files.dontAsk, res)
			ELSE
				MakeFileName(modName, FName, HFext); FilesDelete(FName, CFsubdir);
				MakeFileName(modName, FName, SFext); FilesDelete(FName, SFsubdir);
				MakeFileName(modName, FName, BFext); BFile.Register(FName, "*", Files.dontAsk, res)
			END
		END ;
		HFile := NIL; BFile := NIL; HIFile := NIL; newSFile := NIL; oldSFile := NIL;
		R[0] := NIL; R[1] := NIL; R[2] := NIL; newSF.ConnectTo(NIL); oldSF.ConnectTo(NIL);
		IF inC # NIL THEN Models.EndScript(inC.text, script); inC := NIL END
	END CloseFiles;

	PROCEDURE GetDefaultDirectories;
	BEGIN
		Dialog.MapString( "#ofront:directories", options.dirs)
	END GetDefaultDirectories;

	PROCEDURE PromoteIntConstToLInt*();
	BEGIN
		(* ANSI C does not need explicit promotion.
			K&R C implicitly promotes integer constants to type int in parameter lists.
			if the formal parameter, however, is of type long, appending "L" is required in ordere to promote
			the parameter explicitly to type long (if LONGINT corresponds to long, which we do not really know).
			It works for all known K&R versions of ofront and K&R is dying out anyway.
			A cleaner solution would be to cast with type (LONGINT), but this requires a bit more changes.
		*)
		IF ~(ansi IN opt) THEN Write("L") END
	END PromoteIntConstToLInt;

	PROCEDURE SetWorkDir* (
		IN workdir, subsysdir: ARRAY OF CHAR; useObj, useSym: BOOLEAN; IN addTo: ARRAY 3 OF CHAR);
	VAR
		i: INTEGER;
	BEGIN
		workDir := workdir$;
		i := LEN(workDir$) - 1; IF (i >= 0) & ((workDir[i] = "\") OR (workDir[i] = "/")) THEN workDir[i] := 0X END;
		subsys := subsysdir$;
		i := LEN(subsys$) - 1; IF (i >= 0) & ((subsys[i] = "\") OR (subsys[i] = "/")) THEN subsys[i] := 0X END;
		SFsubdir := ""; CFsubdir := "";
		IF useObj THEN CFsubdir := "/Obj" + addTo END;
		IF useSym THEN SFsubdir := "/Sym" + addTo ELSIF useObj THEN SFsubdir := "/Obj" + addTo END;
	END SetWorkDir;

BEGIN
	subsys := ""; workDir := ""; SFsubdir := ""; CFsubdir := "";
	locate.num := 0; GetDefaultDirectories
END OfrontOPM.


OfrontCmd OfrontOPP OfrontOPB OfrontOPC OfrontOPV OfrontOPT OfrontOPS OfrontOPM
OfrontBrowser
